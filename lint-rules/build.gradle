apply plugin: 'java'

targetCompatibility = '1.7'
sourceCompatibility = '1.7'

repositories {
    jcenter()
}

dependencies {
    compile 'com.android.tools.lint:lint-api:24.2.1'
    compile 'com.android.tools.lint:lint-checks:24.2.1'
}

jar {
    manifest {
        attributes 'Manifest-Version': 1.0
        attributes 'Lint-Registry': 'com.example.MyIssueRegistry'
    }
}

defaultTasks 'assemble'

/**
 * Copies the jar file containing your lint module
 * into the .android/lint directory in your home folder.
 *
 * @see Doug Stevenson's comments at the bottom of this article.
 * */
task copyLintJar(type: Copy) {
    description = 'Copies the lint jar file into the {user.home}/.android/lint folder.'
    from('build/libs/')
    into(System.getProperty("user.home") + '/.android/lint')
    include("*.jar")
}

// Runs the copyLintJar task after build has completed.
build.finalizedBy(copyLintJar)